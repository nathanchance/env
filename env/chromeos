# SPDX-License-Identifier: GPL-3.0-or-later
#
# Copyright (C) 2018-2019 Nathan Chancellor
#
# Utility and terminal setup functions for my Pixelbook


# Setup function
#
# Using crouton (https://goo.gl/fd3zc):
# $ sudo install -Dt /usr/local/bin -m 755 ~/Downloads/crouton
# $ sudo crouton -r buster -t cli-extra
# $ sudo enter-chroot
# $ sudo sh -c 'apt update && apt install -y --no-install-recommends curl zsh && chsh -s /bin/zsh nathan'
#
# Using crostini:
# $ vmc start termina
# $ vmc container termina penguin
# $ sudo sh -c 'apt update && apt install -y --no-install-recommends zsh && chsh -s /bin/zsh natechancellor'
# $ exit
# $ vsh termina penguin
# $ ln -s /mnt/chromeos .
function bootstrap() {(
    # Grab common functions/variables
    source <(curl -LSs https://github.com/nathanchance/scripts/raw/master/common) &>/dev/null

    # Check hostname to see if we are using Crostini (default) or Crouton
    [[ "$(uname -n)" = "localhost" ]] && CROSTINI=false

    # Make sure keys are present
    if ${CROSTINI:=true}; then
        # crostini locations (mount them in via Files, USB first, SD card second, Google Drive third)
        for FOLDER in "removable/USB Drive/Keys" "removable/SD Card/Keys" "GoogleDrive/MyDrive/Crostini"; do
            KEYS=/mnt/chromeos/"${FOLDER}"
            [[ -d ${KEYS} ]] && break
        done
    else
        # crouton folder options (USB first, SD card second, Google Drive third)
        for KEYS in "/var/host/media/removable/USB Drive/Keys" "/var/host/media/removable/SD Card/Keys" "${HOME}/Downloads/Crostini"; do
            [[ -d ${KEYS} ]] && break
        done
    fi
    [[ ! -d ${KEYS} ]] && die "Key folders not found!"

    # Ensure that packages are all up to date
    sudo apt update
    sudo apt upgrade -y

    # Install packages from Debian repos
    sudo apt install -y --no-install-recommends \
        build-essential \
        bc \
        bison \
        devscripts \
        flex \
        git \
        git-email \
        gpg \
        irssi \
        jq \
        libevent-dev \
        libncurses-dev \
        libssl-dev \
        mosh \
        most \
        mutt \
        openvpn \
        pass \
        python-pip \
        ssh \
        tmux \
        vim \
        xclip

    # Install speedtest-cli
    sudo curl -LSo /usr/local/bin/speedtest-cli https://raw.githubusercontent.com/sivel/speedtest-cli/master/speedtest.py
    sudo chmod +x /usr/local/bin/speedtest-cli

    # Install latest version of LLVM/Clang
    if [[ ${ARCH:=$(get_arch)} = "amd64" ]]; then
        if grep -q "unstable" /etc/apt/sources.list; then
            REPO=llvm-toolchain
            URL=http://apt.llvm.org/unstable/
        else
            DISTRO=$(cat /etc/os-release | grep VERSION_CODENAME | cut -d= -f2)
            REPO=llvm-toolchain-${DISTRO}
            URL=http://apt.llvm.org/${DISTRO}/
        fi
        curl https://apt.llvm.org/llvm-snapshot.gpg.key | sudo apt-key add -
        grep -q "apt.llvm.org" /etc/apt/sources.list || echo "deb ${URL} ${REPO} main" | sudo tee -a /etc/apt/sources.list
        sudo apt update -qq
        sudo apt install -y --no-install-recommends \
            clang-9 \
            lld-9
    fi

    # Setup SSH and GPG if keys are present
    mkdir -p "${HOME}"/.ssh
    cp "${KEYS}"/id_ed25519{,.pub} "${HOME}"/.ssh
    chmod 600 "${HOME}"/.ssh/id_ed25519

    # Setup GPG
    gpg --pinentry-mode loopback --import "${KEYS}"/{public*,private*}.asc
    gpg --import-ownertrust "${KEYS}"/ownertrust*.asc
    {
        echo "default-cache-ttl 3600"
        echo "max-cache-ttl 86400"
    } > "${HOME}"/.gnupg/gpg-agent.conf
    gpg-connect-agent reloadagent /bye

    # Clone scripts
    local SCRIPTS; SCRIPTS=${HOME}/scripts
    git clone git@github.com:nathanchance/scripts "${SCRIPTS}" || die "Error cloning script!"
    source "${SCRIPTS}"/env/setup && global_gitignore && create_zshrc
    source "${SCRIPTS}"/funcs/bldupdbin && bldupdbin
    path_push "${HOME}/usr/bin"
    source "${SCRIPTS}"/funcs/git && git_setup
    source "${SCRIPTS}"/funcs/pure && pure_prompt

    # Clone repos
    mkdir -p "${HOME}"/repos
    local DOTFILES; DOTFILES=${HOME}/repos/dotfiles
    git clone git@github.com:nathanchance/dotfiles "${DOTFILES}" || die "Error cloning dotfiles!"

    # Clone password store
    git clone git@github.com:nathanchance/password-store.git "${HOME}"/.password-store || die "Error cloning password store!"

    # Copy tmux config
    cp "${DOTFILES}"/chromeos/.tmux.conf "${HOME}"

    # Setup vim
    zsh "${DOTFILES}"/common/vim/vim_setup.sh

    # Decrypt .muttrc
    gpg --pinentry-mode loopback --output "${HOME}"/.muttrc --decrypt "${DOTFILES}"/common/muttrc.gpg

    # We need UTF characters in crouton for the prompt to work
    if [[ "$(uname -n)" = "localhost" ]]; then
        sudo apt install -y --no-install-recommends locales
        sudo dpkg-reconfigure locales
    fi

    # Download Android platform-tools
    curl -LSso "${TMP_ZIP:=$(mktemp --suffix=.zip)}" "https://dl.google.com/android/repository/platform-tools-latest-linux.zip"
    unzip -d "${HOME}" "${TMP_ZIP}"
    rm -rf "${TMP_ZIP}"
) && source "${HOME}"/.zshrc; }


# Laptop specific aliases
function chromeos_aliases() {
    alias ls='ls --color=auto'
    alias mshsvr='mosh nathan@${HETZNER_IP}'
}


# Laptop specific setup
function chromeos_setup() {
    # Until crbug.com/829934 hits stable/beta
    export TZ=/usr/share/zoneinfo/US/Arizona

    path_push "${HOME}/platform-tools"
}
# vi: filetype=zsh
