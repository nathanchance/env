# SPDX-License-Identifier: GPL-3.0-or-later
#
# Copyright (C) 2018-2019 Nathan Chancellor
#
# Utility and terminal setup functions for my Pixelbook


# Setup function
#
# Using crosh:
# $ curl -LSso ~/Downloads/crouton https://github.com/dnschneid/crouton/raw/master/installer/crouton
# $ sudo sh ~/Downloads/crouton -r buster -t cli-extra
# $ sudo enter-chroot
# $ sudo sh -c 'apt update && apt install -y --no-install-recommends curl zsh && chsh -s /bin/zsh'
#
# Using crostini:
# $ vmc start termina
# $ vmc container termina penguin
# $ sudo sh -c 'apt update && apt install -y --no-install-recommands zsh && chsh -s /bin/zsh natechancellor'
# $ vmc stop termina
# $ vmc start termina
# $ vmc container termina penguin
function bootstrap() {(
    # Grab common functions/variables
    source <(curl -LSs https://github.com/nathanchance/scripts/raw/master/common) &>/dev/null
    source <(curl -LSs https://github.com/nathanchance/scripts/raw/master/constants/versions) &>/dev/null
    source <(curl -LSs https://github.com/nathanchance/scripts/raw/master/funcs/pure) &>/dev/null

    # Make sure keys are present
    if [[ "$(uname -n)" = "penguin" ]]; then
        CROSTINI=${HOME}/Crostini
    else
        CROSTINI=${HOME}/Downloads/Crostini
    fi
    [[ ! -d ${CROSTINI} ]] && die "${CROSTINI} not found!"

    # Set architecture variables
    case "$(uname -m)" in
        "aarch64") ARCH=arm64 ;;
        "x86_64") ARCH=amd64 ;;
        *) die "Unsupported architecture!" ;;
    esac

    # Ensure that packages are all up to date
    sudo apt update
    sudo apt upgrade -y

    # Install packages from Debian repos
    sudo apt install -y --no-install-recommends \
        build-essential \
        bc \
        bison \
        devscripts \
        flex \
        git \
        gpg \
        jq \
        libssl-dev \
        mosh \
        most \
        openvpn \
        pass \
        python-pip \
        ssh \
        tmux \
        vim \
        xclip

    # Install speedtest-cli
    sudo curl -LSo /usr/local/bin/speedtest-cli https://raw.githubusercontent.com/sivel/speedtest-cli/master/speedtest.py
    sudo chmod +x /usr/local/bin/speedtest-cli

    # Install latest version of LLVM/Clang
    curl https://apt.llvm.org/llvm-snapshot.gpg.key | sudo apt-key add -
    echo "deb http://apt.llvm.org/$(if [[ "$(cat /etc/os-release)" =~ stretch ]]; then echo stretch; else echo unstable; fi)/ llvm-toolchain$([[ "$(cat /etc/os-release)" =~ stretch ]] && echo "-stretch") main" | sudo tee -a /etc/apt/sources.list
    sudo apt update -qq
    sudo apt install -y --no-install-recommends \
        clang-9 \
        lld-9

    # Build updated ccache
    curl -LSs https://www.samba.org/ftp/ccache/ccache-"${CCACHE_VERSION:?}".tar.gz | tar -xzf -
    cd ccache-"${CCACHE_VERSION}" || die "Downloading or extracting ccache failed!"
    ./configure
    make -j"$(nproc)"
    sudo make install
    cd "${OLDPWD}" || die "${OLDPWD} doesn't exist??"
    rm -rf ccache*

    # Setup git
    git config --global core.editor vim
    git config --global user.name "Nathan Chancellor"
    git config --global user.email "natechancellor@gmail.com"

    # Install hub
    curl -LSsO https://github.com/github/hub/releases/download/v"${HUB_VERSION:?}"/hub-linux-${ARCH}-"${HUB_VERSION}".tgz
    tar -xf hub-linux-${ARCH}-"${HUB_VERSION}".tgz
    cd hub-linux-${ARCH}-"${HUB_VERSION}" || die "Downloading or extracting hub failed!"
    sudo ./install
    cd "${OLDPWD}" || die "${OLDPWD} doesn't exist??"
    rm -rf hub*

    # Install prebuilt ripgrep and fd binaries if available
    if [[ ${ARCH} = "amd64" ]]; then
        for URL in https://github.com/sharkdp/bat/releases/download/v${BAT_VERSION:?}/bat_${BAT_VERSION}_amd64.deb \
                   https://github.com/sharkdp/fd/releases/download/v${FD_VERSION:?}/fd_${FD_VERSION}_amd64.deb \
                   https://github.com/BurntSushi/ripgrep/releases/download/${RG_VERSION:?}/ripgrep_${RG_VERSION}_amd64.deb; do
            curl -LSsO "${URL}"
            sudo dpkg -i "$(basename "${URL}")" && rm "$(basename "${URL}")"
        done
    # Build them from source otherwise
    else
        # Install rust
        curl https://sh.rustup.rs -sSf | sh
        source "${HOME}"/.cargo/env

        for PAIR in fd:https://github.com/sharkdp/fd/archive/v${FD_VERSION:?}.zip \
                    ripgrep:https://github.com/BurntSushi/ripgrep/archive/${RG_VERSION:?}.zip; do
            BINARY=${PAIR%%:*}
            URL=${PAIR#*:}
            TMP=$(mktemp)

            curl -LSs "${URL}" -o "${TMP}"
            unzip "${TMP}"
            cd "${BINARY}"-* || die "Downloading or extracting ${BINARY} failed!"
            cargo build --release
            cargo install
            cd "${OLDPWD}" || die "${OLDPWD} doesn't exist??"
            rm -rf "${OLDPWD}" "${TMP}"
        done
    fi

    # Setup SSH and GPG if keys are present
    mkdir -p "${HOME}"/.ssh
    mv "${CROSTINI}"/id_ed25519{,.pub} "${HOME}"/.ssh
    chmod 600 "${HOME}"/.ssh/id_ed25519

    # Setup GPG
    gpg --import "${CROSTINI}"/{public*,private*}.asc
    gpg --import-ownertrust "${CROSTINI}"/ownertrust*.asc
    git config --global commit.gpgsign true
    git config --global user.signingkey 2437CB76E544CB6AB3D9DFD399739260CB6CB716
    {
        echo "default-cache-ttl 3600"
        echo "max-cache-ttl 86400"
    } > "${HOME}"/.gnupg/gpg-agent.conf
    gpg-connect-agent reloadagent /bye

    # Clone scripts
    git clone git@github.com:nathanchance/scripts "${HOME}"/scripts || die "Error cloning script!"

    # Clone repos
    mkdir -p "${HOME}"/repos
    local DOTFILES; DOTFILES=${HOME}/repos/dotfiles
    git clone git@github.com:nathanchance/dotfiles "${DOTFILES}" || die "Error cloning dotfiles!"

    # Clone password store
    git clone git@github.com:nathanchance/password-store.git "${HOME}"/.password-store || die "Error cloning password store!"

    # Copy files into their proper locations
    # tmux
    cp "${DOTFILES}"/pixelbook/.tmux.conf "${HOME}"

    # Run small setup scripts from dotfiles
    zsh "${DOTFILES}"/common/global_gitignore.sh
    zsh "${DOTFILES}"/common/vim/vim_setup.sh

    # Install Pure prompt
    pure_prompt

    # We need UTF characters in crouton for the prompt to work
    if [[ "$(uname -n)" = "localhost" ]]; then
        sudo apt install -y --no-install-recommends locale
        sudo dpkg-reconfigure locales
    fi

    # Setup zshrc
    {
        echo
        echo "[[ -f \${HOME}/scripts/env/common ]] && source \"\${HOME}/scripts/env/common\""
        echo "type shell_setup &>/dev/null && shell_setup"
    } > "${HOME}"/.zshrc
) && source "${HOME}"/.zshrc; }


# Laptop specific aliases
function pixelbook_aliases() {
    alias ls='ls --color=auto'
    alias mshsvr='mosh nathan@${RYZEN_IP}'
}


# Setup PS1 and such
function pixelbook_prompt() {
    autoload -U promptinit
    promptinit
    prompt pure
}


# Laptop specific setup
function pixelbook_setup() {
    # Until crbug.com/829934 hits stable/beta
    export TZ=/usr/share/zoneinfo/US/Arizona
    pixelbook_prompt
}
# vi: filetype=zsh
