#!/usr/bin/env fish
# Description: Build and install a version of perf that matches the host machine's kernel version

set workdir (mktemp -d)

# Get host's kernel version
if not set kernel_version (uname -r)
    echo "Failed to get host's kernel version!"
    return 1
end

if not string match -qr '^(?<maj>\d+)\.(?<min>\d+)\.(?<patch>\d+)(?<rc>-rc\d+)?' $kernel_version
    echo "Did not find a kernel version match for host's kernel version: $kernel_version"
    return 1
end

if test -n "$rc"
    set url https://git.kernel.org/torvalds/t/linux-$maj.$min$rc.tar.gz
else
    set ver $maj.$min
    if test $patch -gt 0
        set ver $ver.$patch
    end
    set url https://cdn.kernel.org/pub/linux/kernel/v$maj.x/linux-$ver.tar.xz
end
switch (string match -gr '\.(\w+)$' $url)
    case gz
        set tar_comp_arg -z
    case xz
        set tar_comp_arg -J
    case '*'
        echo "Unhandled compression algorithm: $comp"
        return 1
end

set local_tar $SRCDIR/tc-build-src/(path basename $url)
if test -e $local_tar
    set get_tar_cmd cat $local_tar
else
    set get_tar_cmd curl -LSs $url
end
$get_tar_cmd | tar -C $workdir --strip-components=1 -xf - $tar_comp_arg
or return

if test -e $workdir/tools/bpf/bpftool/sign.c
    echo 'diff --git a/tools/bpf/bpftool/sign.c b/tools/bpf/bpftool/sign.c
index b34f74d210e9..9be9f1190d15 100644
--- a/tools/bpf/bpftool/sign.c
+++ b/tools/bpf/bpftool/sign.c
@@ -30,6 +30,7 @@
 
 static void display_openssl_errors(int l)
 {
+#if 0
 	char buf[OPEN_SSL_ERR_BUF_LEN];
 	const char *file;
 	const char *data;
@@ -45,6 +46,7 @@ static void display_openssl_errors(int l)
 			p_err("OpenSSL %s: %s:%d", buf, file, line);
 		}
 	}
+#endif
 }
 
 #define DISPLAY_OSSL_ERR(cond)				 \
' | patch -d $workdir -p1
    or return
end

pushd $workdir/tools/perf
or return

fish_add_path -P /usr/local/bin

make \
    -f Makefile.perf \
    -j(nproc) \
    prefix=/usr/local \
    LIBPFM4=1 \
    NO_LIBLLVM=1 \
    NO_LIBTRACEEVENT=1 \
    NO_SDT=1 \
    PYTHON=(command -v python3) \
    PYTHON_CONFIG=(command -v python3-config) \
    VF=0 \
    install
or return

popd
rm -fr $workdir
