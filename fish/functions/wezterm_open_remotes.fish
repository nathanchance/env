#!/usr/bin/env fish
# SPDX-License-Identifier: MIT
# Copyright (C) 2022-2023 Nathan Chancellor

function wezterm_open_remotes -d "Open a new wezterm tab for each remote machine I regularly use"
    set fish_path (command -v fish)
    set rg_path (command -v rg)
    set wezterm_path (command -v wezterm)

    # Local machines
    set hosts \
        hetzner:"ğŸŒ Hetzner" \
        aadp:"ğŸŸª AADP" \
        intel-desktop-11700:"ğŸŸ¦ Intel i7-11700" \
        amd-desktop-8745HS:"ğŸŸ¥ AMD mini desktop" \
        intel-laptop:"ğŸŸ¦ Intel laptop" \
        honeycomb:"ğŸŸª Honeycomb" \
        intel-desktop-n100:"ğŸŸ¦ Intel mini desktop" \
        chromebox:"ğŸŸ¦ Chromebox"

    set msh_args $argv

    for item in $hosts
        set host (string split -f 1 : $item)
        set title (string split -f 2 : $item)
        $wezterm_path cli spawn -- $fish_path -c "__wezterm_title $title; msh $msh_args $host; exec $fish_path -l"
    end

    set mac_model (sysctl hw.model | string split -f2 ': ')
    switch $mac_model
        case Mac13,1
            set icon ğŸ–¥ï¸
        case Mac14,2
            set icon ğŸ’»
    end
    $wezterm_path cli spawn -- $fish_path -c "__wezterm_title '$icon macOS'; exec $fish_path -l"
    if command -q orb; and orb list | $rg_path -q fedora
        $wezterm_path cli spawn -- $fish_path -c "__wezterm_title '$icon Fedora'; ssh orb; exec $fish_path -l"
    end
end
