# SPDX-License-Identifier: GPL-3.0-or-later
#
# Copyright (C) 2016-2020 Nathan Chancellor
#
# Helper functions


###############
#             #
#  VARIABLES  #
#             #
###############

# What OS are we on?
HOST=$(uname -n | tr '[:upper:]' '[:lower:]')

# Location of "scripts" folder
if [[ -n ${ZSH_NAME} ]]; then
    SCRIPT=${(%):-%x}
elif [[ -n ${BASH} ]]; then
    SCRIPT=${BASH_SOURCE[0]}
else
    echo "Unsupported shell!"
    return 1
fi
[[ ! ${SCRIPT} =~ /proc ]] && SCRIPTS_FOLDER=$(cd "$(dirname "$(readlink -f "${SCRIPT}")")" || return; pwd)
export SCRIPTS_FOLDER
unset SCRIPT

# Colors
BLINK_RED="\033[05;31m"
BLUE="\033[01;34m"
BOLD="\033[1m"
GRN="\033[01;32m"
RED="\033[01;31m"
RST="\033[0m"
YLW="\033[01;33m"

# Number of make threads
THREADS=$(($(nproc) + 1))
JOBS_FLAG="-j${THREADS}"

# Telegram chat IDs
MARLIN_CHANNEL=-1001169681748
MARLIN_GROUP=-1001132593794
OP6_CHANNEL=-1001088909643
OP6_GROUP=-1001192694857
WAHOO_CHANNEL=-1001354325766
WAHOO_GROUP=-1001298464571


###############
#             #
#  FUNCTIONS  #
#             #
###############

# Alias for echo to print escape codes
function echo() {
    command echo -e "${@}"
}


# Prints a formatted header to point out what is being done to the user
function header() {
    if [[ -n ${2} ]]; then
        COLOR=${2}
    else
        COLOR=${RED}
    fi
    BORDER="====$(for _ in $(seq ${#1}); do printf '='; done)===="
    printf "${COLOR}"
    printf '\n%s\n%s\n%s\n\n' "${BORDER}" "==  ${1}  ==" "${BORDER}"
    printf "${RST}"
}


# Formats the time
function format_time() {
    local TIME_STRING
    MINS=$(((${2} - ${1}) / 60))
    SECS=$(((${2} - ${1}) % 60))
    if [[ ${MINS} -ge 60 ]]; then
        HOURS=$((MINS / 60))
        MINS=$((MINS % 60))
    fi

    if [[ ${HOURS} -eq 1 ]]; then
        TIME_STRING+="1 HOUR, "
    elif [[ ${HOURS} -ge 2 ]]; then
        TIME_STRING+="${HOURS} HOURS, "
    fi

    if [[ ${MINS} -eq 1 ]]; then
        TIME_STRING+="1 MINUTE"
    else
        TIME_STRING+="${MINS} MINUTES"
    fi

    if [[ ${SECS} -eq 1 && -n ${HOURS} ]]; then
        TIME_STRING+=", AND 1 SECOND"
    elif [[ ${SECS} -eq 1 && -z ${HOURS} ]]; then
        TIME_STRING+=" AND 1 SECOND"
    elif [[ ${SECS} -ne 1 && -n ${HOURS} ]]; then
        TIME_STRING+=", AND ${SECS} SECONDS"
    elif [[ ${SECS} -ne 1 && -z ${HOURS} ]]; then
        TIME_STRING+=" AND ${SECS} SECONDS"
    fi

    echo "${TIME_STRING}"
}


# Prints an error in bold red
function display_error() {
    printf "${COLOR:=${RED}}"
    printf '\n%s\n\n' ${1}
    printf "${RST}"
}


# Prints an error in bold red and exits the script
function die() {
    if [[ -n ${TG} ]]; then
        local TMP_FILE
        TMP_FILE=$(mktemp)
        # Info must be resourced so public chats don't get notified
        load_botinfo
        {
            echo "\`\`\`"
            echo "${*}"
            echo "\`\`\`"
        } > "${TMP_FILE}"
        tg_msg "$(cat "${TMP_FILE}")"
        rm -f "${TMP_FILE}"
    fi

    display_error "${@}"
    type pre_exit_commands &>/dev/null && pre_exit_commands
    builtin exit 1
}


# Prints a warning in bold yellow
function warn() {
    COLOR=${YLW} display_error "${@}"
}


# Enforces the value needed for two-part flags
function enforce_value() {
    [[ ${#} -lt 1 ]] && die "A additional value is needed for one of the flags passed to this script!"
}


# Add a remote if it doesn't exist
function add_remote() {
    if ! git remote | grep -q -m 1 "${1}"; then
        git remote add "${1}" "${2}"
    fi
}


# Convert a file location to web link
function web_link() {
    echo "${1}" | sed s/"$(echo "${WEB_FOLDER:?}" | sed 's/\//\\\//g')"/https:\\/\\/nathanchance.me/
}


# Set up a virtual environment for Python
function mkavenv {
    command -v virtualenv2 &>/dev/null || return
    virtualenv2 "${MAIN_FOLDER:?}/venv"
    source "${MAIN_FOLDER}/venv/bin/activate"
}


# Remove virtual environment
function rmvenv {
    command -v deactivate &>/dev/null || return
    deactivate
    rm -rf "${MAIN_FOLDER:?}/venv"
}


# Source Telegram bot information
function load_botinfo() {
    [[ -f ${MAIN_FOLDER:?}/.botinfo ]] && source "${MAIN_FOLDER}"/.botinfo
}


# Telegram notifier function
function tg_msg() {
    if [[ -z ${TOKEN} ]]; then
        [[ -f ${MAIN_FOLDER:?}/.botinfo ]] && source <(cat ${MAIN_FOLDER}/.botinfo | tail -n1)
        if [[ -z ${TOKEN} ]]; then
            display_error "tg_msg() was called but there was no token!"
            return 1
        fi
    fi

    if [[ -z ${CHAT_ID} ]]; then
        [[ -f ${MAIN_FOLDER:?}/.botinfo ]] && source <(cat ${MAIN_FOLDER}/.botinfo | head -n1)
        if [[ -z ${CHAT_ID} ]]; then
            display_error "tg_msg() was called but there was no chat ID!"
            return 1
        fi
    fi

    curl -s -X POST https://api.telegram.org/bot"${TOKEN}"/sendMessage \
            -d chat_id="${CHAT_ID}" \
            -d parse_mode="Markdown" \
            -d text="${*}" 1>/dev/null
}


# Telegram uploader function
function tg_upload() {
    local FILE; FILE=${1}; shift

    if [[ -z ${TOKEN} ]]; then
        display_error "tg_upload() was called but there was no token!"
        return 1
    fi

    if [[ -z ${CHAT_ID} ]]; then
        display_error "tg_upload() was called but there was no chat ID!"
        return 1
    fi

    if [[ ! -f ${FILE} ]]; then
        display_error "tg_upload() failed to find ${FILE}!"
        return 1
    fi

    curl -s -F chat_id="${CHAT_ID}" \
            -F document=@"${FILE}" \
            -F caption="${*}" \
            -X POST https://api.telegram.org/bot"${TOKEN}"/sendDocument 1>/dev/null
}


# Remove URLs from Clang version
function clang_version() {
    [[ ${#} -lt 1 ]] && { display_error "This function takes an argument!"; return; }

    local CLANG POS
    CLANG=${1}
    if [[ $(basename "${CLANG}") != "clang" ]]; then
        CLANG=$(find "${CLANG}" -name clang | head -n1)
        [[ -z ${CLANG} ]] && { display_error "You didn't supply a Clang folder/binary!"; return; }
    else
        [[ ! -f ${CLANG} ]] && { display_error "Clang binary supplied doesn't exist!"; return; }
    fi

    case "${CLANG}" in
        *aosp*) POS="1,2" ;;
        *) POS="1" ;;
    esac
    "${CLANG}" --version | head -n 1 | cut -d \( -f "${POS}" | sed 's/[[:space:]]*$//' || display_error "Something went wrong!"
}


# Is GPG passphrase cached?
# https://demu.red/blog/2016/06/how-to-check-if-your-gpg-key-is-in-cache/
function gpg_key_cached() {
    NUM=$(gpg-connect-agent 'keyinfo --list' /bye 2>/dev/null | awk 'BEGIN{CACHED=0} /^S/ {if($7==1){CACHED=1}} END{if($0!=""){print CACHED} else {print "none"}}')
    if [[ ${NUM} = "none" || ${NUM} -eq 0 ]]; then
        die "Please run 'echo \"test\" | gpg --clearsign &>/dev/null' to cache gpg passphrase!"
    fi
}


# Check if my GPG key can be used
function gpg_key_usable() {
    command -v gpg &>/dev/null || return 1
    gpg --list-secret-keys --keyid-format LONG | grep -q 2437CB76E544CB6AB3D9DFD399739260CB6CB716
}


# ripgrep wrapper
function rg() {
    # Bail out if rg is not installed
    command -v rg &>/dev/null || { warn "ripgrep is not installed!"; return; }

    # Colors match ag
    command rg --colors "path:fg:green" \
               --colors "path:style:bold" \
               --colors "line:fg:yellow" \
               --colors "line:style:bold" \
               --colors "column:fg:yellow" \
               --colors "column:style:bold" \
               --colors "match:fg:black" \
               --colors "match:bg:yellow" \
               "${@}"
}


# Add something to the end of PATH if it isn't already there
function add_to_path_post() {
    local ARRAY ITEM
    IFS=: read -rA ARRAY <<< "${1}"
    for ITEM in $(printf '%s\n' "${ARRAY[@]}" | tac); do
        [[ -d ${ITEM} ]] || continue
        echo "${PATH}" | grep -s -q "${ITEM}" || export PATH="${PATH}:${ITEM}"
    done
}


# Does the same thing as add_to_path_pre but doesn't check if item exists or not
function move_to_front_of_path() {
    local ARRAY ITEM
    IFS=: read -rA ARRAY <<< "${1}"
    for ITEM in $(printf '%s\n' "${ARRAY[@]}" | tac); do
        [[ -d ${ITEM} ]] || continue
        export PATH="${ITEM}:${PATH//${ITEM}:}"
    done
}


# Add something to the beginning of PATH if it isn't already there
function add_to_path_pre() {
    local ARRAY ITEM
    IFS=: read -rA ARRAY <<< "${1}"
    for ITEM in $(printf '%s\n' "${ARRAY[@]}" | tac); do
        [[ -d ${ITEM} ]] || continue
        echo "${PATH}" | grep -s -q "${ITEM}" || export PATH="${ITEM}:${PATH}"
    done
}
# Easy alias
function path_push() { add_to_path_pre "${@}"; }


# Delete the first folder in PATH
function path_pop() {
    export PATH=${PATH#*:}
}


# Set common architecture variable
function get_arch() {(
    case "$(uname -m)" in
        "aarch64") echo "arm64" ;;
        "x86_64") echo "amd64" ;;
        *) exit 1 ;;
    esac
)}


# Get latest release of a GitHub project
function glr() {(
    curl --silent "https://api.github.com/repos/${1:?}/releases/latest" | jq -r .tag_name
)}


# Only use sudo if needed
function sudo_wrapper() {(
    if [[ $(id -u) -eq 0 ]]; then
        "${@}"
    else
        command -v sudo &>/dev/null || exit ${?}
        sudo "${@}"
    fi
)}


# Set distro function
function set_distro() {
    case "$(cat /usr/lib/os-release)" in
        *"Arch Linux"*) ARCH=true ;;
        *"Debian"*) DEBIAN=true ;;
        *"Ubuntu"*) UBUNTU=true ;;
        *) display_error "Distro is not supported"; return 1; ;;
    esac
}


# Common ccache variable
CCACHE=$(path_push "${HOME}/usr/bin"; command -v ccache)


# Decrypt gpg signed file
function decrypt_gpg_file() {(
    [[ -z ${SCRIPTS_FOLDER} && -z ${SCRIPTS} ]] && die "Scripts folder cannot be found!"
    [[ -z ${SCRIPTS_FOLDER} ]] && SCRIPTS_FOLDER=${SCRIPTS}
    gpg --batch --yes --pinentry-mode loopback --output "${HOME}"/."${1:?}" --decrypt "${SCRIPTS_FOLDER:?}"/configs/common/"${1}".gpg
)}

# Update server_ip file from scripts folder
function decrypt_serv_ip() { decrypt_gpg_file server_ip; }


# Update del_dog_api_key file from scripts folder
function decrypt_del_dog_api_key() { decrypt_gpg_file del_dog_api_key; }


# vi: filetype=zsh
