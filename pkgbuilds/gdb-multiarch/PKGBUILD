# Maintainer: Nathan Chancellor <nathan@kernel.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb-multiarch
pkgver=16.2
pkgrel=1
pkgdesc='The GNU Debugger'
arch=(x86_64)
url='https://www.gnu.org/software/gdb/'
license=(GPL-3.0-or-later)
depends=(glibc ncurses libncursesw.so gcc-libs expat xz mpfr source-highlight
         readline libreadline.so guile python libelf)
makedepends=(texinfo boost)
backup=(etc/gdb/gdbinit)
options=(!lto) # too many errors at the moment
source=(https://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.xz{,.sig})
sha1sums=('ddc8916d192286ff41e58c108e0f6cf55cb0e2f5'
          'SKIP')
b2sums=('480cc5c116e418d7eb13a68bee8a2ced8e7a40bd38826dde3590a8d5b690c3dd46be6fa3790518b39ac7255698a1ecf35a2bb51d7e38d849f7b9277c15fe38fc'
        'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker

build() {
  cd gdb-$pkgver

  mkdir -p build && cd build
  ../configure \
    --prefix=/usr \
    --disable-nls \
    --enable-languages=all \
    --enable-source-highlight \
    --enable-targets=all \
    --enable-tui \
    --with-system-readline \
    --with-python=/usr/bin/python \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make
}

package() {

  cd gdb-$pkgver/build
  make -C gdb DESTDIR="$pkgdir" install
  make -C gdbserver DESTDIR="$pkgdir" install
  make -C gdb/data-directory DESTDIR="$pkgdir" install

  # install "custom" system gdbinit
  install -dm 755 "$pkgdir/etc/gdb"
  touch "$pkgdir/etc/gdb/gdbinit"

  # Symlink 'gdb' binary to 'gdb-multiarch' for interoperability purposes
  ln -s gdb "$pkgdir/usr/bin/$pkgname"
}

# vim: ts=2 sw=2 et:
