# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=cvise
pkgver=2.4.0
pkgrel=1
pkgdesc="Super-parallel Python port of the C-Reduce"
arch=(x86_64)
url="https://github.com/marxin/cvise"
license=('custom:University of Illinois Open Source License')
depends=(clang llvm python python-pebble python-psutil)
makedepends=(cmake flex unifdef)
checkdepends=(python-pytest-flake8)
source=("$pkgname-$pkgver.tar.gz::https://github.com/marxin/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('55ae8c39bdbaddba9a2ac1173bef7995e58387bc81f4610125dd1488c8e8b1ae')

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/$pkgname \
    -DCMAKE_PREFIX_PATH=/usr \
    -Wno-dev

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
