# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=cvise
pkgver=2.8.0
pkgrel=2
pkgdesc="Super-parallel Python port of the C-Reduce"
arch=(x86_64)
url="https://github.com/marxin/cvise"
license=('custom:University of Illinois Open Source License')
depends=(
  clang
  llvm-libs
  python
  python-chardet
  python-pebble
  python-psutil
  unifdef
)
makedepends=(
  cmake
  flex
  llvm
)
checkdepends=(
  python-pytest
  python-pytest-flake8
)
optdepends=('colordiff: Show diffs in color')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marxin/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('cb0bd15885b18b4e79be216c6ca7bed546defc0e9b533d6103868580c690a1a1')

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/$pkgname \
    -DCMAKE_PREFIX_PATH=/usr \
    -Wno-dev

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
