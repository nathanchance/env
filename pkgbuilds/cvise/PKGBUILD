# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=cvise
pkgver=2.3.0
pkgrel=1
pkgdesc="Super-parallel Python port of the C-Reduce"
arch=(x86_64)
url="https://github.com/marxin/cvise"
license=('custom:University of Illinois Open Source License')
depends=(clang llvm python python-pebble python-psutil)
makedepends=(cmake flex unifdef)
checkdepends=(python-pytest-flake8)
source=("$pkgname-$pkgver.tar.gz::https://github.com/marxin/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('882b6078da208a9b05d272089c0b1aa41991cf9ee2c3c33b7f0f8a6091e1bb3a')

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/$pkgname \
    -DCMAKE_PREFIX_PATH=/usr \
    -Wno-dev

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
