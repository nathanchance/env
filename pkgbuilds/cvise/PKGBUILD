# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=cvise
pkgver=2.11.0
pkgrel=1
pkgdesc="Super-parallel Python port of the C-Reduce"
arch=(x86_64)
url="https://github.com/marxin/cvise"
license=('NCSA')
depends=(
  clang
  llvm-libs
  python
  python-chardet
  python-pebble
  python-psutil
  unifdef
)
makedepends=(
  cmake
  flex
  llvm
)
checkdepends=(
  python-pytest
)
optdepends=('colordiff: Show diffs in color')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marxin/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('7e3e473843aa79afb98f581d2e100efa47db80df3a961565b691d7b4a4ebd14b')

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/$pkgname \
    -DCMAKE_PREFIX_PATH=/usr \
    -Wno-dev

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
