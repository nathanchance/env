# Maintainer: Nathan Chancellor <nathan@kernel.org>

pkgbase=cross-binutils
pkgname=(
  arm-linux-gnueabi-binutils
  mips-linux-gnu-binutils
  powerpc64-linux-gnu-binutils
  s390x-linux-gnu-binutils
  sparc64-linux-gnu-binutils
)
pkgver=2.46
# https://inbox.sourceware.org/binutils/b30e780b-abc4-437b-bdcf-bb743a1c9eea@redhat.com/
_tar_pkgver=$pkgver.0
pkgrel=1
arch=(x86_64)
url='https://gnu.org/software/binutils/'
license=(GPL-2.0-or-later GPL-3.0-or-later LGPL-2.0-or-later LGPL-3.0-or-later GFDL-1.3 FSFAP)
depends=(glibc libelf zlib zstd)
makedepends=(setconf)
source=("https://mirrors.kernel.org/sourceware/binutils/releases/binutils-$_tar_pkgver.tar.xz")
sha512sums=('32f880bb4f69351f4ae54a5d00359625c6c49d8e76624fb5cffdf174c79c8d3212f66225b81c12933c6ed59604ab652560773dd92fab384b930c97a9d4e1fdf2')

prepare() {
  setconf binutils-$_tar_pkgver/libiberty/configure ac_cpp "'\$CPP \$CPPFLAGS -O2'"
}

_build() {
  local _target="$1"
  shift

  local _build="binutils-$_tar_pkgver/build/$_target"
  mkdir -p "$_build"

  (
    cd "$_build"

    unset CPPFLAGS
    ../../configure --disable-nls \
                    --disable-werror \
                    --enable-colored-disassembly \
                    --enable-deterministic-archives \
                    --enable-multilib \
                    --enable-plugins \
                    --prefix=/usr \
                    --target=$_target \
                    --with-gnu-as \
                    --with-gnu-ld \
                    --with-sysroot=/usr/$_target \
                    --with-system-zlib \
                    "$@"
  )
  make -C "$_build" -O
}

build() {
  _build arm-linux-gnueabi

  _build mips-linux-gnu \
         --enable-targets=mips64-linux-gnueabi64,mips64-linux-gnueabin32

  _build powerpc64-linux-gnu \
         --enable-targets=powerpc-linux-gnu

  _build s390x-linux-gnu \
         --enable-targets=s390-linux-gnu

  _build sparc64-linux-gnu \
         --enable-targets=sparc-linux-gnu
}

_package() {
  make -C "binutils-$_tar_pkgver/build/$1" DESTDIR="$pkgdir" install

  # Remove info documents that conflict with host version
  rm -r "$pkgdir/usr/share/info"

  rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so
}

package_arm-linux-gnueabi-binutils() {
  pkgdesc='A set of programs to assemble and manipulate binary and object files for the ARM EABI target'

  _package arm-linux-gnueabi
}

package_mips-linux-gnu-binutils() {
  pkgdesc='A set of programs to assemble and manipulate binary and object files for the MIPS (32-bit and 64-bit) target'

  _package mips-linux-gnu
}

package_powerpc64-linux-gnu-binutils() {
  pkgdesc='A set of programs to assemble and manipulate binary and object files for the PowerPC (32-bit and 64-bit) target'

  _package powerpc64-linux-gnu
}

package_s390x-linux-gnu-binutils() {
  pkgdesc='A set of programs to assemble and manipulate binary and object files for the SystemZ target'

  _package s390x-linux-gnu
}

package_sparc64-linux-gnu-binutils() {
  pkgdesc='A set of programs to assemble and manipulate binary and object files for the SPARC (32-bit and 64-bit) target'

  _package sparc64-linux-gnu
}
