# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=python-msgspec
_name=${pkgname#python-}
pkgver=0.19.0
pkgrel=1
pkgdesc="A fast serialization and validation library, with builtin support for JSON, MessagePack, YAML, and TOML"
arch=(x86_64)
url="https://github.com/jcrist/msgspec"
license=(BSD-3-Clause)
depends=(
    glibc
    python
    python-attrs
    python-typing_extensions
)
makedepends=(
    git
    python-build
    python-installer
    python-setuptools
    python-setuptools-scm
    python-wheel
)
checkdepends=(
    mypy
    python-msgpack
    python-pytest
)
optdepends=(
  'python-tomli-w: for TOML support'
  'python-tomli: for TOML support'
  'python-yaml: for YAML support'
)
#source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_name}"

  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${_name}"
  # cd "${_name}-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "${_name}"
  # cd "${_name}-$pkgver"
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  # Error on 'import pyright' even with pyright in checkdepends
  rm tests/typing/test_pyright.py

  PYTHONDONTWRITEBYTECODE=1 PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest
}

package() {
  cd "${_name}"
  # cd "${_name}-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
