# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=tuxmake
pkgver=1.6.0
pkgrel=1
pkgdesc='Thin wrapper to build Linux kernels'
url='https://tuxmake.org/'
license=('MIT')
arch=('any')
depends=('perl' 'python')
makedepends=('python-build' 'python-docutils' 'python-flit' 'python-installer' 'python-wheel')
checkdepends=('clang' 'git' 'lld' 'llvm' 'python-pytest' 'python-pytest-mock')
optdepends=('docker: Container-based build support'
            'podman: Container-based build support'
            'socat: Offline build support')
source=("$pkgname-v$pkgver.tar.gz::https://gitlab.com/Linaro/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('8de1b94a956918c410472b0392076fa2f2f1a1572d10460f6c25dbeec1b5566a')

build() {
  cd "$pkgname-v$pkgver"

  make man
  make bash_completion

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-v$pkgver"

  PYTHONDONTWRITEBYTECODE=1 pytest
}

package() {
  cd "$pkgname-v$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dvm644 tuxmake.1 -t "$pkgdir"/usr/share/man/man1
  install -Dvm644 bash_completion/tuxmake -t "$pkgdir"/usr/share/bash-completion/completions
  install -Dvm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
