# Maintainer: Nathan Chancellor <nathan@kernel.org>

_target=s390x-linux-gnu
pkgname=$_target-binutils
pkgver=2.42
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files for the ARM EABI target'
arch=(x86_64)
url='https://gnu.org/software/binutils/'
license=(GPL-2.0-or-later GPL-3.0-or-later LGPL-2.0-or-later LGPL-3.0-or-later GFDL-1.3 FSFAP)
depends=(glibc libelf zlib zstd)
makedepends=(setconf)
source=("https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz")
sha512sums=('155f3ba14cd220102f4f29a4f1e5cfee3c48aa03b74603460d05afb73c70d6657a9d87eee6eb88bf13203fe6f31177a5c9addc04384e956e7da8069c8ecd20a6')

prepare() {
  setconf binutils-$pkgver/libiberty/configure ac_cpp "'\$CPP \$CPPFLAGS -O2'"
}

build() {
  cd "binutils-$pkgver"

  unset CPPFLAGS
  ./configure --disable-nls \
              --enable-colored-disassembly \
              --enable-deterministic-archives \
              --enable-gold \
              --enable-ld=default \
              --enable-multilib \
              --enable-plugins \
              --enable-targets=s390-linux-gnu \
              --prefix=/usr \
              --target=$_target \
              --with-gnu-as \
              --with-gnu-ld \
              --with-sysroot=/usr/$_target \
              --with-system-zlib
  make -O
}

package() {
  make -C "binutils-$pkgver" DESTDIR="$pkgdir" install

  # Remove info documents that conflict with host version
  rm -r "$pkgdir/usr/share/info"

  rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so
}
