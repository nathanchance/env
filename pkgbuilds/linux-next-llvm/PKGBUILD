# Maintainer: Nathan Chancellor <nathan@kernel.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_localversion=-llvm
pkgname=linux-next$_localversion
_srctag=next-20230725
pkgver=${_srctag//-/.}
pkgrel=1
pkgdesc='linux-next'
url="https://git.kernel.org/pub/scm/linux/kernel/git/next/linux-next.git/log/?h=$_srctag"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc
  cpio
  libelf
  pahole
  perl
  tar
  xz
)
options=('!strip')
source=(
  "$pkgname::git+https://git.kernel.org/pub/scm/linux/kernel/git/next/linux-next.git?signed#tag=$_srctag"
  config
  20230718_nathan_drm_i915_avoid_wconstant_logical_operand_in_nsecs_to_jiffies_timeout.patch
  20230724_arnd_btrfs_remove_unused_pages_processed_variable.patch
  20230725_nathan_input_mcs_touchkey_fix_uninitialized_use_of_error_in_mcs_touchkey_probe.patch
  revert-drm_exec-context-for-amdgpu.patch
)

validpgpkeys=(
  '985B681FA45919699753A26489F91C0A41D5C07A'  # Stephen Rothwell
  '3F2568AAC26998F9E813A1C5C3F436CA30F5D8EB'  # Mark Brown
)
sha256sums=('SKIP'
            'e26bfc8e99252acaa9481d161fe2af40f1705a32af3afe4ef65ffed1d445ff80'
            'fa045588e652d952f982e0ae192ae3e5ff4a51d4a3ab22dd518dd39737611fda'
            '06fedd508c51e336b41715e0c852a6e8b956aa83fba4e3623502684b5fc8c289'
            '4f78a483972782562f4102c23a3463b56b06b8515f0ceaa3b6fc79ed1511e6df'
            '21cb45ceff66766aa2c79549056b7e8d5ca64758bc311ac6a96ecb6c50c75c4e')

# This obviously ties this PKGBUILD to my machine but that is kind of the point :)
_kmake() {
  set -x
  make \
    -skj"$(nproc)" \
    HOSTLDFLAGS=-fuse-ld=lld \
    LLVM=${LLVM:-$CBL_TC_LLVM/} \
    LOCALVERSION= \
    "$@"
  set +x
}

prepare() {
  cd $pkgname

  echo "Setting version..."
  echo "${_localversion}" > localversion.10-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  _kmake olddefconfig prepare
  diff -u ../config .config || :

  _kmake -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $pkgname
  _kmake all
}

package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('crda: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
  replaces=(virtualbox-guest-modules-arch wireguard-arch)

  cd $pkgname
  local kernver="$(<version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  ZSTD_CLEVEL=19 _kmake INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build and source links
  rm "$modulesdir"/{source,build}
}

# vim:set ts=8 sts=2 sw=2 et:
