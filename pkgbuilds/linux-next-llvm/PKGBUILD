# Maintainer: Nathan Chancellor <nathan@kernel.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_localversion=-llvm
pkgname=linux-next$_localversion
_srctag=next-20230809
pkgver=${_srctag//-/.}
pkgrel=1
pkgdesc='linux-next'
url="https://git.kernel.org/pub/scm/linux/kernel/git/next/linux-next.git/log/?h=$_srctag"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc
  cpio
  libelf
  pahole
  perl
  tar
  xz
)
options=('!strip')
source=(
  "$pkgname::git+https://git.kernel.org/pub/scm/linux/kernel/git/next/linux-next.git?signed#tag=$_srctag"
  config
  20230724_arnd_btrfs_remove_unused_pages_processed_variable.patch
  20230731_ckoenig_leichtzumerken_drm_exec_use_unique_instead_of_local_label.patch
  20230809_aweber_kernel_backlight_lp855x_drop_ret_variable_in_brightness_change_function.patch
  20230809_nathan_asoc_sof_intel_initialize_chip_in_hda_sdw_check_wakeen_irq.patch
  20230811_seanjc_x86_retpoline_don_t_clobber_rflags_during_srso_safe_ret.patch
  cbe8ded48b939b9d55d2c5589ab56caa7b530709.patch
  cbl-1911.patch
  v3_20430803_xingtong_wu_platform_x86_siemens_simatic_ipc_batt_fix_logical_error_for_bx_59a.patch
  v3_20430803_xingtong_wu_platform_x86_siemens_simatic_ipc_fix_logical_error_for_bx_59a.patch
)

validpgpkeys=(
  '985B681FA45919699753A26489F91C0A41D5C07A'  # Stephen Rothwell
  '3F2568AAC26998F9E813A1C5C3F436CA30F5D8EB'  # Mark Brown
)
sha256sums=('SKIP'
            'ae979ebfff8e5ea8f79b3de356da8c272819d4971018c2db33a3c6d5f6f0ec5e'
            '06fedd508c51e336b41715e0c852a6e8b956aa83fba4e3623502684b5fc8c289'
            '2b86f98bbd75daf3e6350281c2c6f225d8cabce5386adcdd1ad0babdecc7a8ef'
            '0112abfe98bc568c9f4e0637b25c523dffb9058f18ff886c79f71c2a87933de4'
            '9836a5fc6961de920f2d514f755aadd4448d975618a86b1ac79fb1185c342974'
            'ab0f32367622b2d0a4dfe1112fa6ace9d4accbe0a5c7b0623157956f9895ad95'
            '2823756823d9460d010d228615ac92bf82c702a105b0fa517c562a54fb5bffc3'
            'c05c5ce85564cdd85e32846be7f0b0bc0f064c4eef9376ff507135b50885ad38'
            '94be5b43886db3948a0119c9ef462462c6d5560b8ea576b83d2333f8ea3fa0e6'
            '1fc1e1bd8ac18bab80affd344df877876d96187af798219dc4b7744d5946e7ef')

# This obviously ties this PKGBUILD to my machine but that is kind of the point :)
_kmake() {
  set -x
  make \
    -skj"$(nproc)" \
    HOSTLDFLAGS=-fuse-ld=lld \
    LLVM=${LLVM:-$CBL_TC_LLVM/} \
    LOCALVERSION= \
    "$@"
  set +x
}

prepare() {
  cd $pkgname

  echo "Setting version..."
  echo "${_localversion}" > localversion.10-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  _kmake olddefconfig prepare
  diff -u ../config .config || :

  _kmake -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $pkgname
  _kmake all
}

package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('crda: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
  replaces=(virtualbox-guest-modules-arch wireguard-arch)

  cd $pkgname
  local kernver="$(<version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  ZSTD_CLEVEL=19 _kmake INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build and source links
  rm "$modulesdir"/{source,build}
}

# vim:set ts=8 sts=2 sw=2 et:
