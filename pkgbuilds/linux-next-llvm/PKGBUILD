# Maintainer: Nathan Chancellor <nathan@kernel.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_localversion=-llvm
pkgname=linux-next$_localversion
_srctag=next-20220505
pkgver=${_srctag//-/.}
pkgrel=1
pkgdesc='linux-next'
url="https://git.kernel.org/pub/scm/linux/kernel/git/next/linux-next.git/log/?h=$_srctag"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc libelf pahole cpio perl tar xz
)
options=('!strip')
source=(
  "$pkgname::git+https://git.kernel.org/pub/scm/linux/kernel/git/next/linux-next.git?signed#tag=$_srctag"
  config
  0001-fixup-mm-shmem-convert-shmem_swapin_page-to-shmem_sw.patch
  0002-fixup-mm-shmem-turn-shmem_alloc_page-into-shmem_allo.patch
  0003-fixup-mark-CONFIG_DRM_AMDGPU-as-depending-on-CONFIG_.patch
  0004-drm-amdgpu-gfx11-Add-missing-break.patch
  0005-drm-amdgpu-gfx10-Avoid-uninitialised-variable-index.patch
  0006-drm-amdgpu-gfx11-Avoid-uninitialised-variable-index.patch
  0007-drm-i915-fix-null-pointer-dereference.patch
)

validpgpkeys=(
  '985B681FA45919699753A26489F91C0A41D5C07A'  # Stephen Rothwell
  '3F2568AAC26998F9E813A1C5C3F436CA30F5D8EB'  # Mark Brown
)
sha256sums=('SKIP'
            'fd702693dbddbd9bd885f8e71dc4e925dc82ff8f16ac98882adc210df1cbd182'
            '15b966719546279c2c8c848ca93e2a8259743d559c2bdea56856acda9bf10be1'
            '66c0d33729dbbaf81d9fd6ff5ff70d1d372a6a538766763e0e1c3ce83e9074af'
            '0662a77f6cdab88bb63cb9d11b47867935564a8564ef77821ba34800c294d078'
            '09af3f3b904577614f1266d0651adfbe17aa10b0f76126dc7a406c8b23c3503f'
            'ed48284da2c77d990310743cedb3f5b4336227275135e2253df1ce4808ea91a1'
            '3808b1b1bb46699da46b41ee86e14cd66be3a28e763f2a98fffe290ada683799'
            '35491410e07c3ec6f6e979bcba59b6a1fdbb146213df58e03c382caf46c35619')

# This obviously ties this PKGBUILD to my machine but that is kind of the point :)
_kmake() {
  set -x
  make -skj"$(nproc)" HOSTLDFLAGS=-fuse-ld=lld LLVM=${LLVM:-$CBL_TC_BIN/} "$@"
  set +x
}

prepare() {
  cd $pkgname

  echo "Setting version..."
  scripts/setlocalversion --save-scmversion
  echo "${_localversion}" > localversion.10-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  _kmake olddefconfig
  diff -u ../config .config || :

  _kmake -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $pkgname
  _kmake all
}

package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('crda: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
  replaces=(virtualbox-guest-modules-arch wireguard-arch)

  cd $pkgname
  local kernver="$(<version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  _kmake INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build and source links
  rm "$modulesdir"/{source,build}
}

# vim:set ts=8 sts=2 sw=2 et:
