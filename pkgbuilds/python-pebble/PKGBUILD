# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=python-pebble
_name=${pkgname#python-}
pkgver=5.1.0
pkgrel=1
pkgdesc="A neat API to manage threads and processes within an application"
arch=('any')
url="https://github.com/noxdafox/pebble"
license=(LGPL-3.0-or-later)
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/noxdafox/${_name}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('d4b9bd857fe34b287e522fd2f206e53b495b4650d57837fe4fa8c8c5b854c668')

build() {
  cd "${_name}-$pkgver"

  python setup.py build
}

check() {
  cd "${_name}-$pkgver"

  PYTHONDONTWRITEBYTECODE=1 PYTHONPATH=. pytest
}

package() {
  cd "${_name}-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}
