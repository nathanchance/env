# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=python-pebble
_name=${pkgname#python-}
pkgver=5.1.2
pkgrel=2
pkgdesc="A neat API to manage threads and processes within an application"
arch=('any')
url="https://github.com/noxdafox/pebble"
license=(LGPL-3.0-or-later)
depends=(python)
makedepends=(
    python-build
    python-installer
    python-setuptools
    python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/noxdafox/${_name}/archive/refs/tags/$pkgver.tar.gz"
        revert-22774ed99b9588e237ed79ca20b8f21190677b93.patch)
sha256sums=('c3cc53b5ad9ab321733bb7790194bc300fd97e6a0bddfe84a8a0691bb1c1ddae'
            'c79744b27119c4a181e16cd80f289ce09d8064d4cf007e33c1223c4acaaf999b')

prepare() {
  cd "${_name}-$pkgver"

  patch -N -p1 -i ../revert-22774ed99b9588e237ed79ca20b8f21190677b93.patch
}

build() {
  cd "${_name}-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "${_name}-$pkgver"

  PYTHONDONTWRITEBYTECODE=1 PYTHONPATH=. pytest
}

package() {
  cd "${_name}-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
