# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=python-pebble
_name=${pkgname#python-}
pkgver=5.2.0
pkgrel=1
pkgdesc="A neat API to manage threads and processes within an application"
arch=('any')
url="https://github.com/noxdafox/pebble"
license=(LGPL-3.0-or-later)
depends=(python)
makedepends=(
    python-build
    python-installer
    python-setuptools
    python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/noxdafox/${_name}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('6b5d7b4b05cfda53aae55fc43aaf29421e0c45a6eac57ed52ddf3041bc73c7fc')

build() {
  cd "${_name}-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "${_name}-$pkgver"

  PYTHONDONTWRITEBYTECODE=1 PYTHONPATH=. pytest
}

package() {
  cd "${_name}-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
