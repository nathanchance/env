# Maintainer: Nathan Chancellor <nathan@kernel.org>
pkgname=python-pebble
_name=${pkgname#python-}
pkgver=5.0.6
pkgrel=1
pkgdesc="A neat API to manage threads and processes within an application"
arch=('any')
url="https://github.com/noxdafox/pebble"
license=('GPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/noxdafox/${_name}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('128a2033589a77b9479dcc4cc1b20a45b861b8d163bf6c65b914f3b201bd0e12')

build() {
  cd "${_name}-$pkgver"

  python setup.py build
}

check() {
  cd "${_name}-$pkgver"

  PYTHONDONTWRITEBYTECODE=1 PYTHONPATH=. pytest
}

package() {
  cd "${_name}-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}
