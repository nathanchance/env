# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan Fader <jan.fader@web.de>

pkgname=fish
_repo=$pkgname-shell
pkgver=4.0.0
pkgrel=0
pkgdesc='Smart and user friendly shell intended mostly for interactive use'
url='https://fishshell.com/'
arch=('x86_64')
license=('GPL-2.0-only AND BSD-3-Clause AND ISC AND MIT AND PSF-2.0')
depends=('glibc' 'gcc-libs' 'ncurses' 'pcre2')
optdepends=('python: man page completion parser / web config tool'
            'pkgfile: command-not-found hook')
makedepends=('cargo' 'cmake' 'git' 'python-sphinx' 'jq')
checkdepends=('expect' 'procps-ng')
install=fish.install
options=(!lto) # breaks the build and it is not necessary
backup=(etc/fish/config.fish)
source=("git+https://github.com/$_repo/$_repo#tag=$pkgver")
sha256sums=('c74de668e315aeb34b5dc1ec1cb4f9710556114b927223651cbe1e69bdd960b3')
sha512sums=('c2aebf7443cb5aff351eb13252a666478160bafbd1703f3f2a2a061abe85252bfbf79a07127f86a525905b892e5ed89561abfc90a090070df2db0262850b3051')

pkgver() {
  git -C $_repo describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd $_repo

  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_DOCS=True \
    -Wno-dev
  make -C build VERBOSE=1
}

check() {
  cd $_repo

  make -C build fish_run_tests
}

package() {
  cd $_repo

  make -C build DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
