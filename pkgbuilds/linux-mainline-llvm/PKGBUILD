# Maintainer: Nathan Chancellor <nathan@kernel.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_localversion=-llvm
pkgname=linux-mainline$_localversion
### _srctag=v6.5-rc5
### pkgver=${_srctag//-/.}
_srccommit=cacc6e22932f373a91d7be55a9b992dc77f4c59b
pkgver=v6.5.rc5.56.gcacc6e22932f
pkgrel=1
pkgdesc='Linux'
### url="https://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git/log/?h=$_srctag"
url="https://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git/log/?h=$_srccommit"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc
  cpio
  libelf
  pahole
  perl
  tar
  xz
)
options=('!strip')
###   "$pkgname::git+https://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git?signed#tag=$_srctag"
source=(
  "$pkgname::git+https://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git#commit=$_srccommit"
  config
  https://github.com/pop-os/system76-io-dkms/raw/master/system76-io.c
  https://github.com/pop-os/system76-io-dkms/raw/master/system76-io_dev.c
  https://github.com/pop-os/system76-io-dkms/raw/master/system76-io_hwmon.c
  20230809_ndesaulniers_x86_srso_fix_build_breakage_for_ld_ld_lld.patch
  aeedd3a82678d89627bdd020a28e0af35ff45552.patch
  srso-lto-fix.patch
)
validpgpkeys=(
  'ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linus Torvalds
)
sha256sums=('SKIP'
            '4d49a091fe0c6d4e4a79c4d78210c055160b5fdef396b26806f35ad479a46641'
            'd569ee2fbd072a09e89196779ccc0dfd008d5a79a0cec5fa80cc7ed99a3027fa'
            '7276b7fb345f6a5156c594969a01e1ab63e864e0b3ed646bf5132b8301fa4975'
            '26b42e2a48cc4666ad6f85eb4d0fe87f30ea0097d6c1cff4f45e0e3330382916'
            '238ed3aaa0eaae9e0c771b2a52967d8b021de3fd6d72661f76b21caabeb21550'
            'd5fe1870892599763aa35a2aef56c0f465c758ca6ab2cc3b2f2d4a662ff5c6bf'
            'f787ccc35e9182235a33e30dc2fdb5cf3733c646f81e9f4999e9050062ce5d91')

# This obviously ties this PKGBUILD to my machine but that is kind of the point :)
_kmake() {
  set -x
  make \
    -skj"$(nproc)" \
    HOSTLDFLAGS=-fuse-ld=lld \
    LLVM=${LLVM:-$CBL_TC_LLVM/} \
    LOCALVERSION= \
    "$@"
  set +x
}

prepare() {
  cd $pkgname

  echo "Setting version..."
  echo "${_localversion}" > localversion.10-pkgname
  # Future versions of scripts/setlocalversion mess with this, so
  # just do it here
  if head=$(git rev-parse --verify HEAD 2>/dev/null); then
    if [ -z "$(git describe --exact-match 2>/dev/null)" ]; then
      if atag="$(git describe 2>/dev/null)"; then
        echo "$atag" | awk -F- '{printf("-%05d", $(NF-1))}'
      fi
      # Add -g and exactly 12 hex chars.
      printf '%s%s' -g "$(echo $head | cut -c1-12)"
    fi >localversion.20-git
  fi

  for file in system76-io{,_dev,_hwmon}.c; do
    cp -v "$(readlink -f ../$file)" drivers/hwmon
  done
  echo "obj-m += system76-io.o" >> drivers/hwmon/Makefile

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  _kmake olddefconfig
  diff -u ../config .config || :

  _kmake -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $pkgname
  _kmake all
}

package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('crda: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
  replaces=(virtualbox-guest-modules-arch wireguard-arch)

  cd $pkgname
  local kernver="$(<version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  ZSTD_CLEVEL=19 _kmake INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build and source links
  rm "$modulesdir"/{source,build}
}

# vim:set ts=8 sts=2 sw=2 et:
