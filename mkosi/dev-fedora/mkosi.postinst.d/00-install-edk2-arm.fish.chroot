#!/usr/bin/env fish
# Description: Download and install edk2-arm from Debian, as the Fedora package was sunset:
#              https://src.fedoraproject.org/rpms/edk2/c/53fa21dafcef14a7d264a89c2977cdbef58df770

if test "$ARCHITECTURE" != arm64
    return 0
end

set edk2_ver (curl -fLSs 'https://salsa.debian.org/qemu-team/edk2/-/raw/debian/latest/debian/changelog?ref_type=heads' | string match -gr '^edk2 \((.*)\) unstable;' | head -1)
or return
set edk2_deb qemu-efi-arm_"$edk2_ver"_all.deb

set workdir (mktemp -d)
and curl -fLSso $workdir/$edk2_deb http://http.us.debian.org/debian/pool/main/e/edk2/$edk2_deb
and ar x --output $workdir $workdir/$edk2_deb
and tar -C $workdir -xJf $workdir/data.tar.xz
and install -Dvm644 $workdir/usr/share/AAVMF/AAVMF32_CODE.fd /usr/share/edk2/arm/QEMU_EFI.fd
and rm -fr $workdir
