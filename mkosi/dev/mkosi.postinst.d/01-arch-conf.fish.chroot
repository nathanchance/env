#!/usr/bin/env fish

if test "$DISTRIBUTION" != arch
    echo Only relevant for Arch Linux, skipping...
    exit 0
end

for conf in makepkg pacman
    set old /etc/$conf.conf
    set pacnew /etc/$conf.conf.pacnew
    set new /etc/$conf.conf.new

    if test -f $pacnew
        mv -v $pacnew $new
    else
        cp -v $old $new
    end

    switch $conf
        case makepkg
            sed -i 's/^#MAKEFLAGS="-j2"/MAKEFLAGS="-j$(nproc)"/g' $new
            sed -i 's/^!ccache/ccache/g' $new

        case pacman
            sed -i 's/^CheckSpace/#CheckSpace/g' $new
            sed -i 's/^#VerbosePkgLists/VerbosePkgLists/g' $new
            sed -i 's/^#Color/Color/g' $new
            sed -i 's/^NoProgressBar/#NoProgressBar/g' $new
            sed -i 's/^#ParallelDownloads = 5/ParallelDownloads = 7/g' $new
            sed -i "/\[core-testing\]/,/Include/"'s/^#//' $new
            sed -i "/\[extra-testing\]/,/Include/"'s/^#//' $new

            echo '
[nathan]
SigLevel = Optional TrustAll
Server = https://raw.githubusercontent.com/nathanchance/arch-repo/main/$arch' >>$new
    end

    if command -q git
        git diff --no-index $old $new
    else
        diff -Naur $old $new
    end

    mv -v $new $old
end
