#!/usr/bin/env fish

function downgrade_pkg
    set pkg $argv[1]
    set ver $argv[2]

    set tarball /tmp/$pkg-$ver-x86_64.pkg.tar.zst
    set url https://archive.archlinux.org/packages/(string split -f 1 '' $pkg)/$pkg/(basename $tarball)

    curl -LSso $tarball $url
    and pacman -U --noconfirm $tarball
    and rm -fr $tarball
end

switch $DISTRIBUTION
    case arch
        reflector \
            --country "United States" \
            --latest 15 \
            --protocol https \
            --save /etc/pacman.d/mirrorlist \
            --sort rate

        and cat /etc/pacman.d/mirrorlist

        and pacman-key --init
        and pacman-key --populate

        # Temporarily disable the pacman sandbox
        and sed -i 's/^DownloadUser/#DownloadUser/g' /etc/pacman.conf

        and pacman -Syyuu --noconfirm

        # Switch to doas from sudo non-interatively
        and pacman -S --ask 4 --noconfirm opendoas-sudo
        and echo 'session         optional        pam_umask.so' | tee -a /etc/pam.d/doas

        and pacman -S --noconfirm \
            arm-linux-gnueabi-binutils \
            cvise \
            debhelper \
            mips-linux-gnu-binutils \
            powerpc64-linux-gnu-binutils \
            s390x-linux-gnu-binutils \
            shellcheck-bin

        # Turn the pacman sandbox back on
        and sed -i 's/^#DownloadUser/DownloadUser/g' /etc/pacman.conf

    case debian
        # Setup gh repo
        set gh_gpg_key /etc/apt/keyrings/githubcli-archive-keyring.gpg
        and curl -fLSs https://cli.github.com/packages/(basename $gh_gpg_key) | dd of=$gh_gpg_key
        and echo "Types: deb
URIs: https://cli.github.com/packages
Suites: stable
Components: main
Signed-By: $gh_gpg_key" >/etc/apt/sources.list.d/github-cli.sources

        # Setup LLVM repo
        and set llvm_main_version (curl -fLSs https://raw.githubusercontent.com/llvm/llvm-project/main/cmake/Modules/LLVMVersion.cmake | string match -gr 'set\(LLVM_VERSION_MAJOR (\d+)\)'; or echo 20)
        and set llvm_stable_version (math $llvm_main_version - 1)
        and switch $ARCHITECTURE
            case '*'
                set tc_packages clang ll{d,vm}

            case arm64 x86-64
                set llvm_gpg_key /etc/apt/keyrings/apt-llvm-org.gpg
                curl -fLSs https://apt.llvm.org/llvm-snapshot.gpg.key | gpg --dearmor -o $llvm_gpg_key
                and echo "Types: deb
URIs: https://apt.llvm.org/$RELEASE/
Suites: llvm-toolchain
Components: main
Signed-By: $llvm_gpg_key" >/etc/apt/sources.list.d/llvm-main.sources
                and echo "Types: deb
URIs: https://apt.llvm.org/$RELEASE/
Suites: llvm-toolchain-$llvm_stable_version
Components: main
Signed-By: $llvm_gpg_key" >/etc/apt/sources.list.d/llvm-stable.sources
                and set tc_packages {clang,lld,llvm}-$llvm_stable_version

                # Apply workaround for apt.llvm.org installation failures :(
                # https://github.com/llvm/llvm-project/issues/153385
                and if string match -qr "sha1\.second_preimage_resistance = 2026\-02\-01" </usr/share/apt/default-sequoia.config 2>/dev/null
                    mkdir -p /etc/crypto-policies/back-ends

                    string replace 'sha1.second_preimage_resistance = 2026-02-01' 'sha1.second_preimage_resistance = 2027-02-01' \
                        </usr/share/apt/default-sequoia.config \
                        >/etc/crypto-policies/back-ends/apt-sequoia.config
                end
        end

        # Download packages from repos
        and apt update
        and apt install -y \
            $tc_packages \
            gh

    case fedora
        # Setup external repos
        curl -LSso /etc/yum.repos.d/gh-cli.repo https://cli.github.com/packages/rpm/gh-cli.repo
        or return

        if command -q dnf5
            set dnf dnf5
        else
            set dnf dnf
        end

        $dnf update -y
        or return

        $dnf install -y \
            gh
end
