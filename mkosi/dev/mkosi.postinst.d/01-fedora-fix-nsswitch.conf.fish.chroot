#!/usr/bin/env fish
# This script enables the nss-systemd plugin for the shadow and gshadow
# databases, which allows systemd-nspawn's '--bind-user' to passthrough
# the host user's login information to the container. This has been done
# in authselect upstream
#
#   https://github.com/authselect/authselect/commit/50b659f408ae8a6f87e066a7aef5685a1e3e7d10
#
# but that may not be present so do it manually if it is not.

if test "$DISTRIBUTION" != fedora
    echo Only relevant for Fedora, skipping...
    exit 0
end

if string match -qr '^shadow:.*systemd' </usr/share/authselect/default/local/nsswitch.conf
    echo https://github.com/authselect/authselect/commit/50b659f408ae8a6f87e066a7aef5685a1e3e7d10 present already, skipping...
    exit 0
end

set profile local-systemd
authselect create-profile \
    -b local \
    --symlink-{dconf,meta,pam} \
    $profile
or return

set nsswitch_conf /etc/authselect/custom/$profile/nsswitch.conf
sed -i 's;^shadow:     files$;shadow:     files systemd;' $nsswitch_conf
sed -i 's;^gshadow:    files$;gshadow:    files systemd;' $nsswitch_conf

git --no-pager diff --no-index /etc/authselect/nsswitch.conf $nsswitch_conf

authselect select custom/$profile
or return

authselect apply-changes
